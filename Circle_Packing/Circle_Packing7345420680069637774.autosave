ArrayList<Circle> circles;
PImage img;

void setup()
{  
  size(900,400);
  img = loadImage("2017.png");
  circles = new ArrayList<Circle>();
}

void draw()
{
  background(img);
  

  int total = 10;
  int count = 0;
  int attemps = 0;

  while (count < total)
  {
  	Circle newC = newCircle();
  	if (newC != null)
  	{
  		circles.add(newC);
  		count ++;
  	}
  	attemps ++;
  	if (attemps > 1000)
  	{
  		noLoop();
  		println("FINISHED");
  		break;
  	}
  }



  for (Circle c : circles)
  {
  	if (c.growing)
  	{
  		if (c.edges())
  		{
  			c.growing = false;
  		}
  		else
  		{
  			for (Circle other : circles)
  			{
  				if (c != other)
  				{
  					float d = dist(c.x ,c.y ,other.x ,other.y);
  					if (d - 2 < c.r + other.r)
  					{
  						c.growing = false;
  						break;
  					}
  				}

  			}
  		}
  	}

  	c.show();
  	c.grow();
  }
}

Circle newCircle()
{
	float x = random(width);
	float y = random(height);

	boolean valid = true;

	for (Circle c : circles)
	{
		float d = dist(x ,y ,c.x ,c.y);
		if (d < c.r)
		{
			valid = false;
			break;
		}
	}

	if (valid)
	{
		return (new Circle(x,y));
	}
	else
	{
		return null;
	}
	
}
